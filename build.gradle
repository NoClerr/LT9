plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
    id 'io.freefair.lombok' version '9.0.0'
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}


dependencies {

    implementation platform('org.jdbi:jdbi3-bom:3.43.0')
    implementation 'org.jdbi:jdbi3-core'
    implementation 'org.jdbi:jdbi3-sqlobject'
    implementation 'org.jdbi:jdbi3-jackson2'
    implementation 'org.jdbi:jdbi3-postgres'

    api 'org.junit.jupiter:junit-jupiter:5.10.0'

    api 'io.rest-assured:rest-assured:5.5.0'
    implementation 'io.rest-assured:json-path:5.5.0'
    implementation 'io.rest-assured:xml-path:5.5.0'

    api 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.19.2'

    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'org.hamcrest:hamcrest:2.2'

     implementation  'io.qameta.allure:allure-rest-assured:2.24.0'

    implementation 'io.qameta.allure:allure-junit5:2.24.0'

    implementation 'io.qameta.allure:allure-assertj:2.29.1'

    implementation 'org.postgresql:postgresql:42.7.2'
}

test {

    afterSuite { desc, result ->
        if (!desc.parent) { // root suite
            if (result.testCount == 0) {
                throw new GradleException("No tests were executed. Check tags filter: ${project.findProperty('tags')}")
            }
        }
    }


    useJUnitPlatform(){
        if (project.hasProperty("tags")) {
            includeTags(project.property("tags") as String)
        }
    }
    maxParallelForks = (project.findProperty("forks") ?: "3").toInteger()

    systemProperties = [:]
}

allure {
    report {
        version = '2.24.0'
    }

    adapter {
        autoconfigure = true
        aspectjWeaver = true
        frameworks {
            junit5 {
                adapterVersion = '2.24.0'
            }
        }
    }
}